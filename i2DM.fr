(* *************************************************************** *)
(* *****                                                     ***** *)
(* *****  FeynRules model file: inelastic Dirac Dark Matter  ***** *)
(* *****                                                     ***** *)
(* *************************************************************** *)

(* ************************** *)
(* *****  Information   ***** *)
(* ************************** *)
M$ModelName = "i2DM";
M$Information = { Authors->{"A. Filimonova","S. Junius"},
                  Date->"20.01.2022", 
                  Emails->{"a.filimonova@nikhef.nl","sam.junius@vub.be"},
                  References->{""},
                  Version->"1.0"
		};


(*
NOTE: 
This model is bases on the Hidden Abelian Higgs Model provided by C. Duhr 
(see https://feynrules.irmp.ucl.ac.be/wiki/HiddenAbelianHiggsModel)

if we take the effect of Z-Zd mixing into account fully consistently, then mZ0 (the Z mass 
before kinetic mixing with the U(1)X gauge boson) does not have to equal mZ (the mass of 
the mass eigenstate that is dominantly Z). 

This means that the SM tree-level relation Cos[Theta_w] = mW/mZ receives a correction, 
since the actual relationship is cw = mW/mZ0. 

If the kinetic mixing parameter is  too large, this model is then in conflict with 
electroweak precision constraints. However, if it is allowed, then there is a genuine
ambiguity as to where to 'push' this shift in parameters from the SM-best-fit value.

In this instance of the HAHM model we push that change into mW, setting the well-measured
sw to the SM-value and letting mW be an output (i.e. an internal parameter). 

Philosophically speaking, this model should only be used for kinetic mixing below
the electroweak precision bound.
*)




(******* Index definitions ********)

IndexRange[ Index[Generation] ] = Range[3]

IndexRange[ Index[Colour] ] = NoUnfold[Range[3]]

IndexRange[ Index[Gluon] ] = NoUnfold[Range[8]]

IndexRange[ Index[SU2W] ] = Unfold[Range[3]]


IndexStyle[Colour, i]

IndexStyle[Generation, f]

IndexStyle[Gluon ,a]

IndexStyle[SUW2 ,k]

(******* Interaction Orders *******)

M$InteractionOrderHierarchy = {
{QCD,1},
{NP,2},
{QED,2}
}


(****************  Parameters *************)

M$Parameters = {

(****** External parameters ******)

   epsilon == {
        ParameterType -> External,
        BlockName -> HIDDEN,
        ParameterName -> epsilon,
        Value -> 0.01,
        InteractionOrder -> {NP, 1},
        Description -> "U(1)D - U(1)Y kinetic mixing parameter"}, 

  ttheta == {
        ParameterType -> External,
        BlockName -> HIDDEN,
        ParameterName -> ttheta,
        Value -> 10^-5,
        InteractionOrder -> {NP, 1},
        Description -> "Tan[theta_D], mixing angle of dark fermions"},
        
	
  \[Alpha]DM1 == {
        ParameterType -> External,
        BlockName -> HIDDEN,
        ParameterName -> aDM1,
        InteractionOrder -> {NP, -2},
        Value -> 10,
        Description -> "Inverse of the U(1)D coupling constant"},
        
   
	   
  (* SM parameters *)

  swsq == {
        ParameterType -> External,
        BlockName -> SMINPUTS,
        Value -> 0.2250,
        Description -> "Sin[Theta_w] SM value"},

  \[Alpha]EWM1== {
        ParameterType -> External,
        BlockName -> SMINPUTS,
        ParameterName -> aEWM1,
        InteractionOrder -> {QED, -2},
        Value -> 127.9,
        Description -> "Inverse of the electroweak coupling constant"},

  Gf == {
        ParameterType -> External,
        BlockName -> SMINPUTS,
        InteractionOrder -> {QED, 2},
        Value -> 1.16639 * 10^(-5),
        Description -> "Fermi constant"},

  \[Alpha]S == {
        ParameterType -> External,
        BlockName -> SMINPUTS,
        ParameterName -> aS,
        InteractionOrder -> {QCD, 2},
        Value -> 0.118,
        Description -> "Strong coupling constant at the Z pole."},


  ymc == {
        ParameterType -> External,
        BlockName -> YUKAWA,
        Value -> 1.42,
        OrderBlock -> {4},
        Description -> "Charm Yukawa mass"},

 ymb == {
        ParameterType -> External,
        BlockName -> YUKAWA,
        Value -> 4.7,
        OrderBlock -> {5},
        Description -> "Bottom Yukawa mass"},

  ymt == {
        ParameterType -> External,
        BlockName -> YUKAWA,
        Value -> 174.3,
        OrderBlock -> {6},
        Description -> "Top Yukawa mass"},

  ymtau == {
        ParameterType -> External,
        BlockName -> YUKAWA,
        Value -> 1.777,
        OrderBlock -> {15},
        Description -> "Tau Yukawa mass"},



   cabi == {
        TeX -> Subscript[\[Theta], c],
        ParameterType -> External,
        BlockName -> CKMBLOCK,
        OrderBlock -> {1},
        Value -> 0.488,
        Description -> "Cabibbo angle"},



(****** Internal Parameters ******)

   (* Weak Mixing *)

   cw == {
        TeX -> Subscript[c, w],
        ParameterType -> Internal,
        Value -> Sqrt[1 - swsq],
        Description -> "Cos of the Weinberg angle"},  

   sw == {
        TeX -> Subscript[s, w],
        ParameterType -> Internal,
        Value -> Sqrt[swsq],
        Description -> "Sin of the Weinberg angle"},

   (* Gauge couplings *)
  
  \[Alpha]EW == {
        ParameterType -> Internal,
        Value -> 1/\[Alpha]EWM1,
        ParameterName -> aEW,
        InteractionOrder -> {QED, 2},
        Description -> "Electroweak coupling constant"},

   ee == {
        TeX -> e,
        ParameterType -> Internal,
        Value -> Sqrt[4 Pi \[Alpha]EW],
        InteractionOrder -> {QED, 1},
        Description -> "Electric coupling constant"},

   gw == {
        TeX -> Subscript[g, w],
        ParameterType -> Internal,
        Value -> ee / sw,
        InteractionOrder -> {QED, 1},
        Description -> "Weak coupling constant"},

   g1 == {
        TeX -> Subscript[g, 1],
        ParameterType -> Internal,
        Value -> ee / cw,
        InteractionOrder -> {QED, 1},
        Description -> "U(1)Y coupling constant"},

   gs == {
        TeX -> Subscript[g, s],
        ParameterType -> Internal,
        Value -> Sqrt[4 Pi \[Alpha]S],
        InteractionOrder -> {QCD, 1},
        ParameterName -> G,
        Description -> "Strong coupling constant"},

  \[Alpha]D == {
        ParameterType -> Internal,
        Value -> 1/\[Alpha]DM1,
        ParameterName -> aD,
        InteractionOrder -> {NP, 2},
        Description -> "U(1)D coupling contant"},

   gD == {
        TeX -> Subscript[g, D],
        ParameterType -> Internal,
        Value -> Sqrt[4 Pi \[Alpha]D],
        InteractionOrder -> {NP, 1},
        Description -> "U(1)D coupling constant"},


  MZ == {
  	ParameterType -> External,
        BlockName -> GAUGEMASS,
        ParameterName -> MZ,
        Value -> 91.188,
        Description -> "Z mass AFTER mixing, i.e. the mass eigenstate"},

   \[Chi] == {
        ParameterType -> Internal,
        Value -> epsilon/cw,
        ParameterName -> chi,
        Description -> "kinetic mixing parameter"}, 

   \[Eta] == {
        ParameterType -> Internal,
        Value -> \[Chi]/Sqrt[1 - (\[Chi])^2] ,
        ParameterName -> eta,
        Description -> "U(1)X - U(1)Y mixing parameter"},
   	
        
(* New scales *)

   SignAux == {
	ParameterType -> Internal,
	Value -> (MZ-MAp)/Sqrt[(MZ-MAp)^2],
	Description -> "Sign depending on MZ-MAp hiearchy"},
       
  MZ0 == {
       ParameterType -> Internal,
       Value -> Sqrt[(MAp^2 + MZ^2 + SignAux*Sqrt[(MAp^2 - MZ^2)^2 - 4 MAp^2 MZ^2 sw^2 \[Eta]^2])/(2 + 2 sw^2 \[Eta]^2)],
       Description -> "Z mass BEFORE mixing"},    

  DZaux == {
	ParameterType ->Internal,
	Value -> SignAux*Sqrt[MAp^4 + MZ^4 - 2 MAp^2 MZ^2 (1 + 2 sw^2 \[Eta]^2)],
	Description -> "Auxilary variable for DZ"},


  \[CapitalDelta]Z =={
       ParameterType -> Internal,
       Value -> 1/(2 MAp^2 MZ^2) (MAp^4 + MZ^4 - MZ^2 DZaux + MAp^2 (-2 MZ^2 sw^2 \[Eta]^2 - DZaux)),
       ParameterName -> DZ,
       Description -> "Ratio of Z0 and X masses"},

  MX =={
       ParameterType -> Internal,
       Value -> Sqrt[\[CapitalDelta]Z]* MZ0,
       Description -> "X mass BEFORE mixing"},


(*W mass is an output*)

MW == {
        TeX -> Subscript[M, W],
        ParameterType -> Internal,
        Value -> cw MZ0,
        Description -> "W mass"},  


(* Higgs sector *)
       
 MH == {
      ParameterType -> External,
      BlockName -> HIGGS,
      ParameterName -> MH,
      Value -> 125,
      Description -> "SM Higgs mass AFTER mixing, i.e. the mass eigenstate"},

  MHS == {
        ParameterType -> External,
        BlockName -> HIDDEN,
        ParameterName -> MHS,
        Value -> 200,
        Description -> "Singlet-like Higgs mass AFTER mixing (i.e. the mass eigenvalue)"},

  SignScalar == {
	ParameterType -> Internal,
	Value -> (MH-MHS)/Sqrt[(MH-MHS)^2],
	Description -> "Sign depending on MH-MHS hiearchy"},
       

  v == {
        ParameterType -> Internal,
        Value -> 1/Sqrt[Gf* Sqrt[2]],
        InteractionOrder -> {QED, -1},
        Description -> "SM Higgs VEV"},

  vD == {
        ParameterType -> Internal,
        Value -> MX Sqrt[1 - \[Chi]^2] /(gD),
        InteractionOrder -> {NP, -1},
        ParameterName -> xi,
        Description -> "Abelian Higgs VEV"},
  
  
  \[Lambda] == {
        ParameterType -> Internal,
        ParameterName -> lam,
        Value -> (MH^2 + MHS^2 + Sqrt[(MH^2 - MHS^2)^2] * SignScalar)/(4 v^2),
        InteractionOrder -> {QED, 2},
        Description -> "SM Higgs self-coupling"},


  \[Rho] == {
        ParameterType -> Internal,
        ParameterName -> rho,
        Value -> (MH^2 + MHS^2 - Sqrt[(MH^2 - MHS^2)^2] * SignScalar)/(4 vD^2),
        InteractionOrder -> {QED, 2},
        Description -> "Abelian Higgs self-coupling"},
        

  \[Mu]SM2 =={
        TeX -> Subsuperscript[\[Mu], SM, 2],
        ParameterType -> Internal,
        Value ->  v^2 \[Lambda],
        ParameterName -> muSM2,
        Description -> "Quadratic SM potential term"},

  \[Mu]H2 =={
        TeX -> Subsuperscript[\[Mu], H, 2],
        ParameterType -> Internal,
        Value ->  vD^2 \[Rho],
        ParameterName -> muH2,
        Description -> "Quadratic abelian potential term"},

(* Mixing parameters *)

   taAux == {
	ParameterType -> Internal,
	Value -> -1 + \[CapitalDelta]Z + sw^2 \[Eta]^2,
	Description -> "Auxilary variable for ta"},

   ta == {
        TeX -> Subscript[t, \[Alpha]],
        ParameterType -> Internal,
        Value ->  -((taAux + SignAux*Sqrt[4 sw^2 \[Eta]^2 + (taAux)^2])/(2 sw \[Eta])),
        Description -> "Tangent of Z-Zd mixing angle"},
   
   ca == {
        TeX -> Subscript[c, \[Alpha]],
        ParameterType -> Internal,
        Value -> (1 + ta^2)^(-1/2),
        Description -> "Cosine of Z-Zd mixing angle"},

   sa == {
        TeX -> Subscript[s, \[Alpha]],
        ParameterType -> Internal,
        Value -> ta (1 + ta^2)^(-1/2),
        Description -> "Sine of Z-Zd mixing angle"},


(* Dark fermions masses *)

\[Lambda]D =={
        TeX -> Subscript[\[Lambda], D],
	ParameterName -> lamD,
        ParameterType -> Internal,
        Value ->-Sqrt[2] ttheta Abs[MD1 - MD2]/(vD (1+ttheta^2)),
        Description -> "Dark fermion mixing in the mass basis"},

  \[CapitalDelta]Mgauge == {
       ParameterType -> Internal,
       ParameterName -> DMgauge,
       Value -> Sqrt[(MD1-MD2)^2-2 vD^2 \[Lambda]D^2],
       Description -> "Mass splitting between the dark fermion gauge eigenstates"},

  MD0 == {
       ParameterType -> Internal,
       Value -> (MD1+MD2-\[CapitalDelta]Mgauge)/2,
       Description -> "Neutral dark fermion mass BEFORE fermion mixing"},

  MDD == {
       ParameterType -> Internal,
       Value -> (MD1+MD2+\[CapitalDelta]Mgauge)/2,
       Description -> "Charged dark fermion mass BEFORE fermion mixing"},

  \[CapitalDelta]M == {
       ParameterType -> Internal,
       ParameterName -> DM,
       Value -> Sqrt[(MDD-MD0)^2+2 vD^2 \[Lambda]D^2],
       Description -> "Physical mass splitting"},



(* Dark fermions rotation *)

  ctheta == {
        TeX -> Subscript[c, \[Theta]],
        ParameterType -> Internal,
        Value -> (1+ttheta^2)^(-1/2),
        Description -> "Cosine of theta_D"},

  stheta == {
        TeX -> Subscript[s, \[Theta]],
        ParameterType -> Internal,
        Value -> ttheta (1+ttheta^2)^(-1/2),
        Description -> "Sine of theta_D"},


        
(* Yukawa sector *)

   yl == {
        Indices -> {Index[Generation]},
        AllowSummation -> True,
        ParameterType -> Internal,
        Value -> {yl[1] -> 0, yl[2] -> 0, yl[3] -> Sqrt[2] ymtau / v},
        ParameterName -> {yl[1] -> ye, yl[2] -> ym, yl[3] -> ytau},
        InteractionOrder -> {QED, 1},
        ComplexParameter -> False,
        Definitions -> {yl[1] -> 0, yl[2] ->0},
        Description -> "Lepton Yukawa coupling"},

   yu == {
        Indices -> {Index[Generation]},
        AllowSummation -> True,
        ParameterType -> Internal,
        Value -> {yu[1] -> 0, yu[2] -> Sqrt[2] ymc / v, yu[3] -> Sqrt[2] ymt / v},
        ParameterName -> {yu[1] -> yu, yu[2] -> yc, yu[3] -> yt},
        InteractionOrder -> {QED, 1},
        ComplexParameter -> False,
        Definitions -> {yu[1] -> 0},
        Description -> "U-quark Yukawa coupling"},

   yd == {
        Indices -> {Index[Generation]},
        AllowSummation -> True,
        ParameterType -> Internal,
        Value -> {yd[1] -> 0, yd[2] -> 0, yd[3] -> Sqrt[2] ymb / v},
        ParameterName -> {yd[1] -> yd, yd[2] -> ys, yd[3] -> yb},
        InteractionOrder -> {QED, 1},
        ComplexParameter -> False,
        Definitions -> {yd[1] -> 0, yd[2] -> 0},
        Description -> "D-quark Yukawa coupling"},


  CKM == {
       Indices -> {Index[Generation], Index[Generation]},
       TensorClass -> CKM,
       Unitary -> True,
       Definitions -> {CKM[3, 3] -> 1,
                       CKM[i_, 3] :> 0 /; i != 3,
                       CKM[3, i_] :> 0 /; i != 3},
       Value -> {CKM[1,2] -> Sin[cabi],
                   CKM[1,1] -> Cos[cabi],
                   CKM[2,1] -> -Sin[cabi],
                   CKM[2,2] -> Cos[cabi]},
       Description -> "CKM-Matrix"}




}


(************** Gauge Groups ******************)

M$GaugeGroups = {

  U1Y == {
        Abelian -> True,
        GaugeBoson -> B,
        Charge -> Y,
        CouplingConstant -> g1},

  U1D == {
        Abelian -> True,
        GaugeBoson -> X,
        Charge -> Qd,
        CouplingConstant -> gD},

  SU2L == {
        Abelian -> False,
        GaugeBoson -> Wi,
        StructureConstant -> Eps,
        CouplingConstant -> gw},

  SU3C == {
        Abelian -> False,
        GaugeBoson -> G,
        StructureConstant -> f,
        SymmetricTensor -> dSUN,
        Representations -> {T, Colour},
        CouplingConstant -> gs}
}

(********* Particle Classes **********)

M$ClassesDescription = {

(********** Fermions ************)
	(* Leptons (neutrino): I_3 = +1/2, Q = 0 *)
  F[1] == {
        ClassName -> vl,
        ClassMembers -> {ve,vm,vt},
        FlavorIndex -> Generation,
	SelfConjugate -> False,
	Indices -> {Index[Generation]},
        Mass -> 0,
        Width -> 0,
	QuantumNumbers -> {LeptonNumber -> 1},
	PropagatorLabel -> {"v", "ve", "vm", "vt"} ,
	PropagatorType -> S,
	PropagatorArrow -> Forward,
        PDG -> {12,14,16},
        FullName -> {"Electron-neutrino", "Mu-neutrino", "Tau-neutrino"} },

	(* Leptons (electron): I_3 = -1/2, Q = -1 *)
  F[2] == {
        ClassName -> l,
        ClassMembers -> {e, m, tt},
        FlavorIndex -> Generation,
	SelfConjugate -> False,
	Indices -> {Index[Generation]},
	Mass -> {Ml, {ME, 0.000511}, {MM, 0.106}, {MTA, 1.777}},
        Width -> 0,
	QuantumNumbers -> {Q -> -1, LeptonNumber -> 1},
	PropagatorLabel -> {"l", "e", "m", "tt"},
	PropagatorType -> Straight,
        ParticleName -> {"e-", "m-", "tt-"},
        AntiParticleName -> {"e+", "m+", "tt+"},
	PropagatorArrow -> Forward,
        PDG -> {11, 13, 15},
        FullName -> {"Electron", "Muon", "Tau"} },

	(* Quarks (u): I_3 = +1/2, Q = +2/3 *)
  F[3] == {
        ClassMembers -> {u, c, t},
        ClassName -> uq,
        FlavorIndex -> Generation,
	SelfConjugate -> False,
	Indices -> {Index[Generation], Index[Colour]},
	Mass -> {Mu, {MU, 0.0022}, {MC, 1.42}, {MT, 174.3}},
        Width -> {0, 0, {WT, 1.50833649}},
	QuantumNumbers -> {Q -> 2/3},
	PropagatorLabel -> {"uq", "u", "c", "t"},
	PropagatorType -> Straight,
	PropagatorArrow -> Forward,
        PDG -> {2, 4, 6}, 
        FullName -> {"u-quark", "c-quark", "t-quark"}},

	(* Quarks (d): I_3 = -1/2, Q = -1/3 *) 
  F[4] == {
        ClassMembers -> {d, s, b},
        ClassName -> dq,
        FlavorIndex -> Generation,
	SelfConjugate -> False,
	Indices -> {Index[Generation], Index[Colour]},
	Mass -> {Md, {MD, 0.0047}, {MS, 0.096}, {MB, 4.7}},
        Width -> 0,
	QuantumNumbers -> {Q -> -1/3},
	PropagatorLabel -> {"dq", "d", "s", "b"},
	PropagatorType -> Straight, 
	PropagatorArrow -> Forward,
        PDG -> {1,3,5},
        FullName -> {"d-quark", "s-quark", "b-quark"} },


(********** Dark Fermions ************)

  F[5] == { ClassName -> Chi1,
            SelfConjugate -> False,
            Mass -> {MD1, 50},
            Width -> {WD1,0},
            PDG -> {52},
            TeX -> Subscript[\[Chi],1],
            ParticleName    -> "~Chi1",
	    AntiParticleName -> "~Chi1~",
            PropagatorType  -> Straight,
            PropagatorArrow -> True,
            FullName -> "Lightest dark fermion" },

  F[6] == { ClassName -> Chi2,
            SelfConjugate -> False,
            Mass -> {MD2, 100},
            Width -> {WD2,1},
            PDG -> {58},
            TeX -> Subscript[\[Chi],2],
            ParticleName    -> "~Chi2",
	    AntiParticleName -> "~Chi2~",
            PropagatorType  -> Straight,
            PropagatorArrow -> True,
            FullName -> "Heavy dark fermion" },

(****** Unphysical Fields before mixing ******)

  F[16] == {
        ClassName   -> \[Psi]0,
        Unphysical -> True,
        SelfConjugate   -> True,
        Definitions -> {\[Psi]0[sp_] ->  ctheta Chi1[sp] + stheta Chi2[sp]},
        FullName -> "Neutral dark fermion before mixing"},

  F[17] == {
        ClassName   -> \[Psi]D,
        Unphysical -> True,
        SelfConjugate   -> False,
        QuantumNumbers -> {Qd -> 1},
        Definitions -> {\[Psi]D[sp_] ->  {-stheta Chi1[sp] + ctheta Chi2[sp]} },
        FullName -> "Charged dark fermion before mixing"},



(********** Ghosts **********)

 	U[1] == {
       ClassName -> ghG,
       SelfConjugate -> False,
       Indices -> {Index[Gluon]},
       Ghost -> G,
       Mass -> 0,
       Width -> 0,
       QuantumNumbers -> {GhostNumber -> 1},
       PropagatorLabel -> uG,
       PropagatorType -> GhostDash,
       PropagatorArrow -> Forward},


(************ Gauge Bosons ***************)
	(* Gauge bosons: Q = 0 *)
  V[1] == {
        ClassName -> A,
	SelfConjugate -> True,
	Indices -> {},
	Mass -> 0,
        Width -> 0,
	PropagatorLabel -> "a",
	PropagatorType -> Sine,
	PropagatorArrow -> None,
        PDG -> 22,
        FullName -> "Photon" },

  V[21] == {
        ClassName -> Z,
	SelfConjugate -> True, 
	Indices -> {},
	Mass -> {MZ, Internal},
        Width -> {WZ, 2.44140351},
	PropagatorLabel -> "Z",
	PropagatorType -> Sine,
	PropagatorArrow -> None,
        PDG -> 23,
        FullName -> "Z boson" },

  V[22] == {
        ClassName -> Ap,
	SelfConjugate -> True, 
	Indices -> {},
	Mass -> {MAp, 150},
        Width -> {WAp, 0.0008252},
	PropagatorLabel -> "Ap",
	PropagatorType -> Sine,
	PropagatorArrow -> None,
        PDG -> 1023,
        FullName -> "Dark Photon" },


	(* Gauge bosons: Q = -1 *)
  V[3] == {
        ClassName -> W,
	SelfConjugate -> False,
	Indices -> {},
	Mass -> {MW, Internal},
        Width -> {WW, 2.04759951},
	QuantumNumbers -> {Q -> 1},
	PropagatorLabel -> "W",
	PropagatorType -> Sine,
	PropagatorArrow -> Forward,
	ParticleName ->"W+",
	AntiParticleName ->"W-",
        PDG -> 24,
        FullName -> "W boson" },

V[4] == {
        ClassName -> G,
	SelfConjugate -> True,
	Indices -> {Index[Gluon]},
	Mass -> 0,
        Width -> 0,
        PropagatorLabel -> G,
	PropagatorType -> C,
	PropagatorArrow -> None,
        PDG -> 21,
        FullName -> "Gluons" },

V[5] == {
        ClassName -> Wi,
        Unphysical -> True,
        Definitions -> {Wi[mu_, 1] -> (W[mu] + Wbar[mu])/Sqrt[2],
                        Wi[mu_, 2] -> (Wbar[mu] - W[mu])/Sqrt[2]/I,
                        Wi[mu_, 3] -> -cw sa Ap[mu] + cw ca  Z[mu] + sw A[mu]},
        SelfConjugate -> True,
        Indices -> {Index[SU2W]},
        FlavorIndex -> SU2W,
        Mass -> 0},

        (* Unphysical gauge bosons *)
 V[210] == {
        ClassName -> Bp,
	SelfConjugate -> True,
        Unphysical -> True, 
	Indices -> {},
	Mass -> 0,
        Width -> 0,
        Definitions -> {Bp[mu_] :> cw A[mu] -sw ca Z[mu] + sw sa Ap[mu]},
        FullName -> "Hypercharge gauge boson before mass mixing"},

  V[220] == {
        ClassName -> Xp,
	SelfConjugate -> True,
        Unphysical -> True, 
	Indices -> {},
	Mass -> 0,
        Width -> 0,
        Definitions -> {Xp[mu_] :> sa Z[mu] + ca Ap[mu]},
        FullName -> "Dark gauge boson before mass mixing"},

V[6] == {
        ClassName -> B,
	SelfConjugate -> True,
        Definitions -> {B[mu_] -> Bp[mu] + \[Eta] Xp[mu]},
	Indices -> {},
	Mass -> 0,
        Unphysical -> True,
        FullName -> "Hypercharge gauge boson"},

V[61] == {
        ClassName -> X,
	SelfConjugate -> True,
        Definitions -> {X[mu_] -> \[Eta]/\[Chi] Xp[mu]},
	Indices -> {},
	Mass -> 0,
        Unphysical -> True,
        FullName -> "Dark gauge boson"},


(************ Scalar Fields **********)
	(* physical Higgs: Q = 0 *) 
  S[11] == {
        ClassName -> h,
	SelfConjugate -> True,
	Mass -> {MH, Internal},
        Width -> {WH, 0.00282299},
	PropagatorLabel -> "h",
	PropagatorType -> D,
	PropagatorArrow -> None,
        PDG -> 25,
        FullName -> "Higgs boson" },

  S[12] == {
        ClassName -> hs,
	SelfConjugate -> True,
	Mass -> {MHS, Internal},
        Width -> {WHS, 5.23795},
	PropagatorLabel -> "hs",
	PropagatorType -> D,
	PropagatorArrow -> None,
        PDG -> 35,
        FullName -> "Dark Higgs boson" },

 
	S[110] == {
        ClassName -> H,
	SelfConjugate -> True,
        Unphysical -> True,
        Definitions -> {H -> hs}},

  S[120] == {
        ClassName -> phih,
	SelfConjugate -> False,
        Unphysical -> True,
        QuantumNumbers -> {Qd -> 1},
        Definitions -> {phih -> (vD - h)/Sqrt[2] }}

}



(*****************************************************************************************)

(* SM Lagrangian *)

(******************** Gauge F^2 Lagrangian terms*************************)
(*Sign convention from Lagrangian in between Eq. (A.9) and Eq. (A.10) of Peskin & Schroeder.*)
 LGauge := ExpandIndices[-1/4 (del[Wi[nu, i1], mu] - del[Wi[mu, i1], nu] + gw Eps[i1, i2, i3] Wi[mu, i2] Wi[nu, i3])*
 					(del[Wi[nu, i1], mu] - del[Wi[mu, i1], nu] + gw Eps[i1, i4, i5] Wi[mu, i4] Wi[nu, i5]) -
	
	1/4 (del[B[nu], mu] - del[B[mu], nu])^2 - 1/4 (del[X[nu], mu] - del[X[mu], nu])^2 +
        \[Chi]/2 (del[X[nu], mu] - del[X[mu], nu]) (del[B[nu], mu] - del[B[mu], nu]) -
	
	1/4 (del[G[nu, a1], mu] - del[G[mu, a1], nu] + gs f[a1, a2, a3] G[mu, a2] G[nu, a3])*
 		 (del[G[nu, a1], mu] - del[G[mu, a1], nu] + gs f[a1, a4, a5] G[mu, a4] G[nu, a5])];


(********************* Fermion Lagrangian terms*************************)
(*Sign convention from Lagrangian in between Eq. (A.9) and Eq. (A.10) of Peskin & Schroeder.*)
 LFermions := Module[{Lkin, LQCD, LBright, LBleft, LWleft, LDfermions},

    Lkin = I uqbar.Ga[mu].del[uq, mu] + 
    	I dqbar.Ga[mu].del[dq, mu] + 
    	I lbar.Ga[mu].del[l, mu] + 
    	I vlbar.Ga[mu].del[vl, mu];

    LQCD = gs (uqbar.Ga[mu].T[a].uq + 
    	dqbar.Ga[mu].T[a].dq)G[mu, a];

    LBright = 
       -2ee/cw B[mu]/2 lbar.Ga[mu].ProjP.l +           (*Y_lR=-2*)
    	4ee/3/cw B[mu]/2 uqbar.Ga[mu].ProjP.uq -       (*Y_uR=4/3*)
    	2ee/3/cw B[mu]/2 dqbar.Ga[mu].ProjP.dq;        (*Y_dR=-2/3*)

    LBleft = 
     -ee/cw B[mu]/2 vlbar.Ga[mu].ProjM.vl -          (*Y_LL=-1*)
    	ee/cw B[mu]/2 lbar.Ga[mu].ProjM.l  +           (*Y_LL=-1*)
    	ee/3/cw B[mu]/2 uqbar.Ga[mu].ProjM.uq +        (*Y_QL=1/3*)
    	ee/3/cw B[mu]/2 dqbar.Ga[mu].ProjM.dq ;        (*Y_QL=1/3*)
	
	LWleft = ee/sw/2(
	   vlbar.Ga[mu].ProjM.vl Wi[mu, 3] -              (*sigma3 = ( 1   0 )*)
    	lbar.Ga[mu].ProjM.l Wi[mu, 3] +                (*         ( 0  -1 )*)
    	
    	Sqrt[2] vlbar.Ga[mu].ProjM.l W[mu] +
    	Sqrt[2] lbar.Ga[mu].ProjM.vl Wbar[mu]+
    	
    	uqbar.Ga[mu].ProjM.uq Wi[mu, 3] -              (*sigma3 = ( 1   0 )*)
    	dqbar.Ga[mu].ProjM.dq Wi[mu, 3] +              (*         ( 0  -1 )*)
    	
    	Sqrt[2] uqbar.Ga[mu].ProjM.CKM.dq W[mu] +
    	Sqrt[2] dqbar.Ga[mu].ProjM.HC[CKM].uq Wbar[mu]
    	);

 (**** Dark Fermion Lagrangian ****)  

        LDfermions = 
                I \[Psi]Dbar.Ga[mu].(del[\[Psi]D, mu] -I gD X[mu] \[Psi]D)
                +I \[Psi]0bar.Ga[mu].del[\[Psi]0, mu]
                -MDD \[Psi]Dbar.\[Psi]D-MD0 \[Psi]0bar.\[Psi]0
                -(\[Lambda]D phih \[Psi]Dbar.\[Psi]0 + HC[\[Lambda]D phih \[Psi]Dbar.\[Psi]0]);

    Lkin + LQCD + LBright + LBleft + LWleft + LDfermions];

(******************** Higgs Lagrangian terms****************************)
 Phi :=  {0, (v + H)/Sqrt[2]};
 Phibar := {0, (v + H)/Sqrt[2]};
 

    
 LHiggs := Block[{PMVec, WVec, Dc, Dcbar, Vphi},
    
    PMVec = Table[PauliSigma[i], {i, 3}];   
    Wvec[mu_] := {Wi[mu, 1], Wi[mu, 2], Wi[mu, 3]};

	(*Y_phi=1*)
    Dc[f_List, mu_] := I del[f, mu] + ee/cw B[mu]/2 f + ee/sw/2 (Wvec[mu].PMVec).f;
    Dcbar[f_List, mu_] := -I del[f, mu] + ee/cw B[mu]/2 f + ee/sw/2 f.(Wvec[mu].PMVec);

    Dc[phih, mu_] := del[phih, mu] -I gD X[mu] phih;
    Dc[phihbar, mu_] := del[phihbar, mu] +I gD X[mu] phihbar;

    Vphi[Phi2SM_, Phi2H_] := -\[Mu]SM2 Phi2SM + \[Lambda] (Phi2SM)^2 -
              \[Mu]H2 Phi2H + \[Rho] (Phi2H)^2;

    (Dcbar[Phibar, mu]).Dc[Phi, mu] + Dc[phihbar,mu] Dc[phih, mu] - Vphi[Phibar.Phi, phihbar phih]];
  
  
  
(*************** Yukawa Lagrangian***********************)
LYuk :=    Module[{s,r,n,m,i},                                                    -
	      yd[n]              dqbar[s,n,i].ProjP[s,r].dq[r,n,i] (v+H)/Sqrt[2]  -
	      yu[n]              uqbar[s,n,i].ProjP[s,r].uq[r,n,i] (v+H)/Sqrt[2]  -
	      yl[n]               lbar[s,n].ProjP[s,r].l[r,n]      (v+H)/Sqrt[2]
           ]

LYukawa := LYuk + HC[LYuk];



(**************Ghost terms**************************)
(* Now we need the ghost terms which are of the form:             *)
(* - g * antighost * d_BRST G                                     *)
(* where d_BRST G is BRST transform of the gauge fixing function. *)

LGhost := 
		Block[{dBRSTG,LGhostG,dBRSTWi,LGhostWi,dBRSTB,LGhostB},
		
	(***********First the pure gauge piece.**********************) 	
	dBRSTG[mu_,a_] := 1/gs Module[{a2, a3}, del[ghG[a], mu] + gs f[a,a2,a3] G[mu,a2] ghG[a3]];
		LGhostG := - gs ghGbar[a].del[dBRSTG[mu,a],mu];
 			 	
	(***********Now add the pieces together.********************)
	LGhostG]
	
(*********Total SM Lagrangian*******)	 	
Li2DM := LGauge + LHiggs + LFermions + LYukawa  + LGhost;
		
		
